class SRB2EnemyTemplate : Actor
{
	Default
	{
		Monster;
		+DONTCORPSE +NOBLOOD +NOTDMATCH
		DeathSound "sfx/pop";
	}
	States
	{
		Death:
			XPLD A 1 {
				A_Scream();
				A_NoBlocking();
				A_Stop();
				bDONTTHRUST = true;
				bNOGRAVITY = true;
			}
			XPLD BCD 5;
			Stop;
	}
}

class SRB2SceneryTemplate : Actor
{
	Default
	{
		+NOCLIP
		+NOBLOCKMAP
		+NOGRAVITY
		+NOINTERACTION
	}
}

class SRB2MonitorTemplate : Actor
{
	Default
	{
		Health 1;
		ReactionTime 8;
		Radius 16;
		Height 32;
		Mass 100;
		+SOLID +SHOOTABLE +NOBLOOD +COUNTITEM
		DeathSound "sfx/pop";
	}
	States
	{
		Death:
			TNT1 A 0;
			Goto Death2;
		Death2:
			TNT1 A 0 {
				A_Scream();
				A_NoBlocking();
				A_Stop();
				bDONTTHRUST = true;
				bNOGRAVITY = true;
			}
			MTEX BCDE 2;
			MTEX F -1;
			Stop;
	}
}
class SRB2MonitorIconTemplate : Actor
{
	void A_ItemBox() {}	// filled in by child objects
	Default
	{
		Threshold 68;
		ReactionTime 10;
		Speed 2;
		Radius 8;
		Height 14;
		Mass 100;
		+NOINTERACTION
	}
	States
	{
		Spawn:
			PRUP Z 0;
			Goto See;
		See:
			#### # 18 A_ChangeVelocity(z: self.Speed);
			#### # 0 A_Stop;
			#### # 18 A_ItemBox;
			Stop;
	}
}

class SRB2SpringTemplate : Actor
{
	override void Touch(Actor toucher)
	{
		A_StartSound("sfx/spring");
		SetState(ResolveState("Touched"));
		if (toucher.FindState("Spring"))
			toucher.SetStateLabel("Spring");
	}
	Default
	{
		Speed 20;
		Radius 20;
		Height 16;
		Mass 100;
		+SPECIAL
	}
}